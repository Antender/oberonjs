MODULE EberonTypes;
IMPORT LanguageContext, Procedure, Types;

TYPE
    CallGenerator = PROCEDURE(cx: LanguageContext.PType; type: Types.DefinedProcedure): Procedure.PCallGenerator;

    MethodType* = RECORD(Procedure.Std)
        PROCEDURE MethodType*(id: STRING; t: Types.PDefinedProcedure; call: CallGenerator);

        PROCEDURE procType*(): Types.PDefinedProcedure;

        type: Types.PDefinedProcedure;
        call: CallGenerator
    END;
    PMethodType* = POINTER TO MethodType;

    MethodVariable* = RECORD(Types.ProcedureId)
    END;

PROCEDURE MethodType.designatorCode(id: STRING): STRING;
    RETURN id
END MethodType.designatorCode;

PROCEDURE MethodType.procType(): Types.PDefinedProcedure;
    RETURN SELF.type
END MethodType.procType;

PROCEDURE MethodType.description(): STRING;
    RETURN "method '" + SELF.name + "'"
END MethodType.description;

PROCEDURE MethodType.callGenerator(cx: LanguageContext.PType): Procedure.PCallGenerator;
    RETURN SELF.call(cx, SELF.type^)
END MethodType.callGenerator;

PROCEDURE MethodType.MethodType(id: STRING; t: Types.PDefinedProcedure; call: CallGenerator)
    | SUPER(id, NIL);
BEGIN
    SELF.type := t;
    SELF.call := call;
END;

PROCEDURE MethodVariable.idType(): STRING;
    RETURN "method"
END MethodVariable.idType;

PROCEDURE makeMethod*(type: Types.PType): Types.PProcedureId;
    RETURN NEW MethodVariable(type);
END;

END EberonTypes.