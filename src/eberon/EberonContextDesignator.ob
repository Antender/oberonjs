MODULE EberonContextDesignator;
IMPORT
    CodeGenerator,
    ContextDesignator, ContextExpression, ContextHierarchy, ContextProcedure,
    EberonConstructor, EberonMap, EberonRtl, EberonString, EberonTypePromotion,
    Errors, Expression, Module, Object, Operator, Procedure, Record, TypeId, Types, Variable;
TYPE
    Type* = RECORD(ContextDesignator.Type)
        procCall: Procedure.PCallGenerator;
    END;
    PType = POINTER TO Type;

    MapElementVariable = RECORD(Types.Variable)
        PROCEDURE MapElementVariable(type: Types.PStorageType; readOnly: BOOLEAN; code: STRING);

        elementType: Types.PStorageType; 
        readOnly: BOOLEAN; 
        code: STRING;
    END;

    ResultVariable = RECORD(Types.Variable)
        PROCEDURE ResultVariable(e: Expression.PType);

        expression: Expression.PType;
    END;

    TypeNarrowVariable* = RECORD(EberonTypePromotion.Variable)
        PROCEDURE TypeNarrowVariable*(type: Types.PStorageType; isRef, isReadOnly: BOOLEAN; code: STRING);

        mType: Types.PStorageType;
        isRef: BOOLEAN;
        readOnly: BOOLEAN;
        code: STRING;
    END;
    PTypeNarrowVariable* = POINTER TO TypeNarrowVariable;

    DereferencedTypeNarrowVariable = RECORD(EberonTypePromotion.Variable)
        PROCEDURE DereferencedTypeNarrowVariable(var: PTypeNarrowVariable);

        var: PTypeNarrowVariable;
    END;

    SelfAsPointer = RECORD(Types.Id)
    END;

    ExpressionProcedureCall* = RECORD(ContextHierarchy.Node)
        PROCEDURE ExpressionProcedureCall(parent: ContextHierarchy.PNode);
    END;

    AssignmentOrProcedureCall* = RECORD(ContextExpression.ExpressionHandler)
        right: Expression.PType;
    END;

    OperatorNew* = RECORD(ContextDesignator.QIdentHandler)
        PROCEDURE handleExpression(e: Expression.PType);

        info: TypeId.PType;
        call: Procedure.PCallGenerator;
    END;

    OperatorNewMsg = RECORD(ContextHierarchy.Message)
        PROCEDURE OperatorNewMsg(e: Expression.PType);

        expression: Expression.PType;
    END;

    PromoteTypeMsg* = RECORD(ContextHierarchy.Message)
        PROCEDURE PromoteTypeMsg*(info: EberonTypePromotion.PVariable; type: Types.PStorageType);

        info-: EberonTypePromotion.PVariable;
        type-: Types.PStorageType;
    END;

    TransferPromotedTypesMsg* = RECORD(ContextHierarchy.Message)
        PROCEDURE TransferPromotedTypesMsg*(p: EberonTypePromotion.PCombined); 

        promotion: EberonTypePromotion.PCombined;
    END;

    GetMethodSelfMsg* = RECORD(ContextHierarchy.Message)
    END;

    GetSelfAsPointerMsg* = RECORD(ContextHierarchy.Message)
    END;

    GetMethodSuperMsg* = RECORD(ContextHierarchy.Message)
    END;

    SuperMethodInfo* = RECORD(Object.Type)
        PROCEDURE SuperMethodInfo*(info: Types.PProcedureId; code: STRING);

        info: Types.PProcedureId;
        code: STRING;
    END;
    PSuperMethodInfo* = POINTER TO SuperMethodInfo;

VAR
    getMethodSelfMsg: GetMethodSelfMsg;
    getSelfAsPointerMsg: GetSelfAsPointerMsg;
    getMethodSuperMsg: GetMethodSuperMsg;

PROCEDURE checkMapKeyType*(type: Types.PType);
BEGIN
    IF (type # EberonString.string) & (~Types.isString(type)) THEN
        Errors.raise("invalid MAP key type: STRING or string literal or ARRAY OF CHAR expected, got '" 
                    + type.description() + "'");            
    END;
END;

PROCEDURE Type.doCheckIndexType(type: Types.PType);
BEGIN
    IF SELF.currentType IS EberonMap.PType THEN
        checkMapKeyType(type);
    ELSE
        SUPER(type);
    END;
END;

PROCEDURE Type.doIndexSequence(info: Types.PId; code, indexCode: STRING): ContextDesignator.PIndex;
VAR
    result: ContextDesignator.PIndex;
BEGIN
    currentType <- SELF.currentType;
    IF currentType = EberonString.string THEN
        result := NEW ContextDesignator.Index(
            0, 
            Types.basic.ch, 
            EberonString.makeElementVariable(),
            ContextDesignator.stringIndexCode(SELF),
            "",
            "");
    ELSIF currentType IS EberonMap.PType THEN
        indexType <- currentType.valueType;
        rtl <- SELF.root().language().rtl(EberonRtl.PType);
        rval <- rtl.getMappedValue(code, indexCode);
        lval <- code + "[" + indexCode + "]";
        var <- NEW MapElementVariable(indexType, info(Types.PVariable).isReadOnly(), rval);
        result := NEW ContextDesignator.Index(0, indexType, var, rval, lval, "");
    ELSE
        result := SUPER(info, code, indexCode);
    END;
    RETURN result;
END;

PROCEDURE Type.doMakeDerefVar(info: Types.PId): Types.PVariable;
VAR
    result: Types.PVariable;
BEGIN
    IF info IS PTypeNarrowVariable THEN
        result := NEW DereferencedTypeNarrowVariable(info);
    ELSE
        result := SUPER(info);
    END;
    RETURN result;
END;

PROCEDURE beginCall(d: PType);
BEGIN
    type <- d.currentType;
    info <- d.info;
    IF (info IS TypeId.PType) & (type IS Types.PRecord) THEN
        cx <- ContextHierarchy.makeLanguageContext(d);
        d.procCall := EberonConstructor.makeConstructorCall(info, cx, FALSE);
        ContextDesignator.discardCode(d^);
    ELSE
        d.procCall := ContextProcedure.makeCall(d, type, info);
    END;
END;

PROCEDURE endCall(VAR d: Type);
BEGIN
    e <- d.procCall.end();
    ContextDesignator.advance(d, e.type(), NEW ResultVariable(e), e.code(), "", FALSE);
    d.procCall := NIL;
END;

PROCEDURE breakTypePromotion*(VAR msg: ContextHierarchy.Message): BOOLEAN;
BEGIN
    result <- FALSE;
    IF msg IS TransferPromotedTypesMsg THEN
        msg.promotion.clear();
        result := TRUE;
    ELSIF msg IS PromoteTypeMsg THEN
        result := TRUE;
    END;
    RETURN result;
END;

PROCEDURE makePointer(type: Types.PStorageType): Record.PPointer;
BEGIN
    typeId <- NEW TypeId.Type(type);
    RETURN NEW Record.Pointer("", typeId);
END;

PROCEDURE Type.handleMessage(VAR msg: ContextHierarchy.Message): Object.PType;
VAR
    result: Object.PType;
BEGIN
    IF msg IS ContextDesignator.BeginCallMsg THEN
        beginCall(SELF(POINTER));
    ELSIF msg IS ContextDesignator.EndCallMsg THEN
        endCall(SELF);
    ELSIF msg IS OperatorNewMsg THEN
        e <- msg.expression;
        ContextDesignator.advance(SELF, e.type(), NEW ResultVariable(e), e.code(), "", FALSE);
    ELSIF ~breakTypePromotion(msg) THEN (* no type promotion after calling functions *)
        result := SUPER(msg);
    END;    

    RETURN result;
END;

PROCEDURE Type.handleExpression(e: Expression.PType);
BEGIN
    IF SELF.procCall # NIL THEN
        SELF.procCall.handleArgument(e);
    ELSE
        SUPER(e);
    END;
END;

PROCEDURE Type.handleLiteral(s: STRING);
BEGIN
    IF s = "SELF" THEN
        type <- SELF.handleMessage(getMethodSelfMsg)(Types.PStorageType);
        info <- NEW Variable.DeclaredVariable("this", type);
        ContextDesignator.advance(SELF, type, info, "this", "", FALSE);
    ELSIF s = "POINTER" THEN
        type <- SELF.handleMessage(getSelfAsPointerMsg)(Types.PStorageType);
        ContextDesignator.advance(SELF, makePointer(type), NEW SelfAsPointer(), "", "", FALSE);
    ELSIF s = "SUPER" THEN
        ms <- SELF.handleMessage(getMethodSuperMsg)(PSuperMethodInfo);
        ContextDesignator.advance(SELF, ms.info.type, ms.info, ms.code, "", FALSE);
    ELSE
        SUPER(s);
    END;
END;

PROCEDURE MapElementVariable.MapElementVariable(type: Types.PStorageType; readOnly: BOOLEAN; code: STRING)
    | elementType(type),
      readOnly(readOnly),
      code(code);
END;

PROCEDURE MapElementVariable.type(): Types.PStorageType;
    RETURN SELF.elementType;
END;

PROCEDURE MapElementVariable.isReference(): BOOLEAN;
    RETURN FALSE;
END;

PROCEDURE MapElementVariable.isReadOnly(): BOOLEAN;
    RETURN SELF.readOnly;
END;

PROCEDURE MapElementVariable.referenceCode(): STRING;
BEGIN
    IF SELF.elementType.isScalar() THEN
        Errors.raise("cannot reference map element of type '" 
                     + SELF.elementType.description() + "'");
    END;
    RETURN SELF.code;        
END;

PROCEDURE MapElementVariable.idType(): STRING;
VAR
    result: STRING;
BEGIN
    result := "MAP's element";
    IF SELF.readOnly THEN
        result := "read-only " + result;
    END;
    RETURN result;
END;

PROCEDURE ResultVariable.ResultVariable(e: Expression.PType)
    | expression(e);
END;

PROCEDURE ResultVariable.type(): Types.PStorageType;
    RETURN SELF.expression.type()(Types.PStorageType);
END;

PROCEDURE ResultVariable.isReference(): BOOLEAN;
    RETURN FALSE;
END;

PROCEDURE ResultVariable.referenceCode(): STRING;
BEGIN
    RETURN "";        
END;

PROCEDURE ResultVariable.isReadOnly(): BOOLEAN;
    RETURN TRUE;
END;

PROCEDURE ResultVariable.idType(): STRING;
VAR
    result: STRING;
BEGIN
    IF SELF.expression.type() # NIL THEN
        result := "result";
    ELSE
        result := "statement";
    END;
    RETURN "procedure call " + result;
END;

PROCEDURE TypeNarrowVariable.TypeNarrowVariable(type: Types.PStorageType; isRef, isReadOnly: BOOLEAN; code: STRING)
    | mType(type),
      isRef(isRef),
      readOnly(isReadOnly),
      code(code);
END;

PROCEDURE TypeNarrowVariable.type(): Types.PStorageType;
    RETURN SELF.mType;
END;

PROCEDURE TypeNarrowVariable.setType(type: Types.PStorageType);
BEGIN
    SELF.mType := type;
END;

PROCEDURE TypeNarrowVariable.isReference(): BOOLEAN;
    RETURN SELF.isRef;
END;

PROCEDURE TypeNarrowVariable.isReadOnly(): BOOLEAN;
    RETURN SELF.readOnly;
END;

PROCEDURE TypeNarrowVariable.referenceCode(): STRING;
BEGIN
    RETURN SELF.code;        
END;

PROCEDURE TypeNarrowVariable.idType(): STRING;
VAR
    result: STRING;
BEGIN
    IF SELF.readOnly THEN
        result := "non-VAR formal parameter";
    ELSE
        result := SUPER();
    END;
    RETURN result;
END;

PROCEDURE DereferencedTypeNarrowVariable.DereferencedTypeNarrowVariable(var: PTypeNarrowVariable)
    | var(var);
END;

PROCEDURE DereferencedTypeNarrowVariable.type(): Types.PStorageType;
    RETURN Record.pointerBase(SELF.var.type()(Record.PPointer)^);
END;

PROCEDURE DereferencedTypeNarrowVariable.setType(type: Types.PStorageType);
BEGIN
    SELF.var.setType(makePointer(type));
END;

PROCEDURE DereferencedTypeNarrowVariable.isReference(): BOOLEAN;
    RETURN TRUE;
END;

PROCEDURE DereferencedTypeNarrowVariable.isReadOnly(): BOOLEAN;
    RETURN FALSE;
END;

PROCEDURE DereferencedTypeNarrowVariable.referenceCode(): STRING;
BEGIN
    RETURN SELF.var.code;        
END;

PROCEDURE SelfAsPointer.idType(): STRING;
    RETURN "SELF(POINTER)";
END;

PROCEDURE ExpressionProcedureCall.ExpressionProcedureCall(parent: ContextHierarchy.PNode)
    | SUPER(parent);
BEGIN
    NEW(SELF.attributes);
END;

PROCEDURE ExpressionProcedureCall.endParse(): BOOLEAN;
VAR
    e: Expression.PType;
BEGIN
    d <- SELF.attributes.designator;
    info <- d.info();
    IF info^ IS ResultVariable THEN
        e := info.expression;
        e := NEW Expression.Type(d.code(), d.type(), NIL, e.constValue(), e.maxPrecedence());
    ELSE
        e := ContextExpression.designatorAsExpression(d);
    END;
    SELF.parent()^(ContextExpression.ExpressionHandler).handleExpression(e);
    RETURN TRUE;
END;

PROCEDURE AssignmentOrProcedureCall.handleExpression(e: Expression.PType);
BEGIN
    SELF.right := e;
END;

PROCEDURE AssignmentOrProcedureCall.codeGenerator(): CodeGenerator.PIGenerator;
    RETURN CodeGenerator.nullGenerator;
END;

PROCEDURE AssignmentOrProcedureCall.endParse(): BOOLEAN;
VAR
    code: STRING;
BEGIN
    d <- SELF.attributes.designator;
    type <- d.type();
    IF SELF.right # NIL THEN
        left <- Expression.make(d.code(), type, d, NIL);
        code := Operator.assign(left, SELF.right, ContextHierarchy.makeLanguageContext(SELF(POINTER)));
    ELSIF ~(d.info()^ IS ResultVariable) THEN
        procCall <- ContextProcedure.makeCall(SELF(POINTER), type, d.info());
        result <- procCall.end();
        Module.assertProcStatementResult(result.type());
        code := d.code() + result.code();
    ELSE
        Module.assertProcStatementResult(type);
        code := d.code();
    END;

    SELF.parent().codeGenerator().write(code);
    RETURN TRUE;
END;

PROCEDURE OperatorNew.handleQIdent(q: ContextHierarchy.QIdent);
BEGIN
    found <- ContextHierarchy.getQIdSymbolAndScope(SELF.root()^, q);
    s <- found.symbol();
    info <- s.info();

    IF ~(info IS TypeId.PType) THEN
        Errors.raise("record type is expected in operator NEW, got '" + info.idType() + "'");
    ELSE
        type <- info.type();
        IF ~(type IS Types.PRecord) THEN
            Errors.raise("record type is expected in operator NEW, got '" + type.description() + "'");
        END;
        SELF.info := info;        
    END;
END;

PROCEDURE OperatorNew.handleExpression(e: Expression.PType);
BEGIN
    SELF.call.handleArgument(e);
END;    

PROCEDURE OperatorNew.handleMessage(VAR msg: ContextHierarchy.Message): Object.PType;
VAR
    result: Object.PType;
BEGIN
    IF msg IS ContextDesignator.BeginCallMsg THEN
        SELF.call := EberonConstructor.makeConstructorCall(
            SELF.info, 
            ContextHierarchy.makeLanguageContext(SELF(POINTER)), 
            TRUE);
    ELSIF msg IS ContextDesignator.EndCallMsg THEN
    ELSE
        result := SUPER(msg);
    END;
    RETURN result;
END;

PROCEDURE OperatorNew.endParse(): BOOLEAN;
BEGIN
    void <- SELF.handleMessage(NEW OperatorNewMsg(SELF.call.end())^);
    RETURN TRUE;
END;

PROCEDURE OperatorNewMsg.OperatorNewMsg(e: Expression.PType)
    | expression(e);
END;

PROCEDURE PromoteTypeMsg.PromoteTypeMsg(info: EberonTypePromotion.PVariable; type: Types.PStorageType)
    | info(info),
      type(type);
END;

PROCEDURE TransferPromotedTypesMsg.TransferPromotedTypesMsg(p: EberonTypePromotion.PCombined)
    | promotion(p); 
END;

PROCEDURE SuperMethodInfo.SuperMethodInfo(info: Types.PProcedureId; code: STRING)
    | info(info),
      code(code);
END;

END EberonContextDesignator.