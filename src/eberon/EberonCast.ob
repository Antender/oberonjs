MODULE EberonCast;
IMPORT Cast, EberonString, EberonTypes, Types;

PROCEDURE isOpenCharArray(type: Types.PType): BOOLEAN;
    RETURN (type IS Types.POpenArray) 
         & (Types.arrayElementsType(type^) = Types.basic.ch)
END isOpenCharArray;

PROCEDURE implicit*(from, to: Types.PType; toVar: BOOLEAN; ops: Cast.Operations; VAR op: Cast.PCastOp): INTEGER;
VAR
    result: INTEGER;
BEGIN
    IF   ((from = EberonString.string) & ((to IS Types.PString) OR isOpenCharArray(to))
      OR ((from IS Types.PString) & (to = EberonString.string))) THEN
        IF toVar THEN 
            result := Cast.errVarParameter;
        ELSE
            result := Cast.errNo;
        END;
    ELSIF (from IS Types.PArray) & (to IS EberonTypes.PDynamicArray)
        & Cast.areTypesExactlyMatch(Types.arrayElementsType(from^), 
                                    Types.arrayElementsType(to^)) THEN
        IF toVar & ~(from IS EberonTypes.PDynamicArray) THEN
            result := Cast.errVarParameter;
        ELSE
            result := Cast.errNo;
        END;
    ELSE
        result := Cast.implicit(from, to, toVar, ops, op);
    END;
    RETURN result
END implicit;

END EberonCast.