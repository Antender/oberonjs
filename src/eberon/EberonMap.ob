MODULE EberonMap;
IMPORT 
    Context, EberonRtl, Expression, EberonString, EberonRecord, EberonTypes, Errors, LanguageContext, Procedure, Record, Types;
CONST
    removeMethodName = "remove";
    clearMethodName = "clear";
TYPE
    Type* = RECORD(Types.Array)
    END;
    PType* = POINTER TO Type;

    Method = RECORD(Procedure.Std)
    END;

    MethodRemoveField = RECORD(EberonTypes.MethodField)
        PROCEDURE MethodRemoveField();
    END;

    MethodClearField = RECORD(EberonTypes.MethodField)
        PROCEDURE MethodClearField();
    END;

    MapMethod = RECORD(Method)
    END;

    MethodRemove = RECORD(MapMethod)
        PROCEDURE MethodRemove();
    END;

    MethodClear = RECORD(MapMethod)
        PROCEDURE MethodClear();
    END;

    MethodCallRemove = RECORD(Procedure.StdCall)
    END;

    MethodCallClear = RECORD(Procedure.StdCall)
    END;

    ElementVariable* = RECORD(Types.Variable)
        PROCEDURE ElementVariable*(type: Types.PStorageType; readOnly: BOOLEAN; lval, rval: STRING);

        elementType-: Types.PStorageType; 
        readOnly: BOOLEAN; 
        lval-, rval-: STRING;
    END;

PROCEDURE Type.initializer(cx: Context.Type): STRING;
    RETURN "{}";
END;

PROCEDURE Type.description(): STRING;
    RETURN "MAP OF " + SELF.elementsType.description();
END;

PROCEDURE Type.denote(id: STRING; isReadObly: BOOLEAN): Types.PField;
VAR
    result: Types.PField;

    PROCEDURE assertReadOnly();
    BEGIN
        EberonRecord.assertNotReadOnly(isReadObly, id, "MAP");
    END;
BEGIN
    IF id = removeMethodName THEN
        assertReadOnly();
        result := NEW MethodRemoveField();
    ELSIF id = clearMethodName THEN
        assertReadOnly();
        result := NEW MethodClearField();
    ELSE
        result := SUPER(id, isReadObly);
    END;
    RETURN result;
END;

PROCEDURE Type.isScalar(): BOOLEAN;
    RETURN FALSE;
END;

PROCEDURE MethodCallRemove.make(args: ARRAY OF Expression.PType; cx: LanguageContext.PType): Expression.PType;
BEGIN
    argCode <- Procedure.makeArgumentsCode(cx);
    arg <- Procedure.checkSingleArgument(args, SELF, cx.language.types, argCode);
    RETURN Expression.makeSimple("[" + argCode.result() + "]", NIL)
END;        

PROCEDURE MethodCallClear.make(args: ARRAY OF Expression.PType; cx: LanguageContext.PType): Expression.PType;
BEGIN
    Procedure.checkArgumentsCount(LEN(args), 0);
    RETURN Expression.makeSimple("", NIL);
END;

PROCEDURE MapMethod.description(): STRING;
    RETURN "MAP's method '" + SELF.name + "'";
END;

PROCEDURE MethodRemove.MethodRemove()
    | SUPER(removeMethodName, NIL);
END;

PROCEDURE MethodClear.MethodClear()
    | SUPER(clearMethodName, NIL);
END;

PROCEDURE MethodRemove.callGenerator(cx: LanguageContext.PType): Procedure.PCallGenerator;
BEGIN
    call <- NEW MethodCallRemove();
    a <- NEW Types.ProcedureArgument(NEW Types.OpenArray(Types.basic.ch), FALSE);
    call.args.add(a);
    RETURN Procedure.makeCallGenerator(call, cx)
END;

PROCEDURE MethodClear.callGenerator(cx: LanguageContext.PType): Procedure.PCallGenerator;
BEGIN
    call <- NEW MethodCallClear();
    RETURN Procedure.makeCallGenerator(call, cx)
END;

PROCEDURE MethodRemoveField.MethodRemoveField()
    | SUPER(NEW MethodRemove());
END;

PROCEDURE MethodClearField.MethodClearField()
    | SUPER(NEW MethodClear());
END;

PROCEDURE MethodRemoveField.designatorCode(leadCode: STRING; cx: Context.Type): Types.PFieldCode;
    RETURN NEW Types.FieldCode("delete " + leadCode, "", "");
END;

PROCEDURE MethodClearField.designatorCode(leadCode: STRING; cx: Context.Type): Types.PFieldCode;
    RETURN NEW Types.FieldCode(cx.rtl()(EberonRtl.PType).clearMap(leadCode), "", "");
END;

PROCEDURE ElementVariable.ElementVariable(type: Types.PStorageType; readOnly: BOOLEAN; lval, rval: STRING)
    | elementType(type),
      readOnly(readOnly),
      lval(lval),
      rval(rval);
END;

PROCEDURE ElementVariable.type(): Types.PStorageType;
    RETURN SELF.elementType;
END;

PROCEDURE ElementVariable.isReference(): BOOLEAN;
    RETURN FALSE;
END;

PROCEDURE ElementVariable.isReadOnly(): BOOLEAN;
    RETURN SELF.readOnly;
END;

PROCEDURE ElementVariable.idType(): STRING;
VAR
    result: STRING;
BEGIN
    result := "MAP's element of type '" + SELF.elementType.description() + "'";
    IF SELF.readOnly THEN
        result := "read-only " + result;
    END;
    RETURN result;
END;

END EberonMap.

