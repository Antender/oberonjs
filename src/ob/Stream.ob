MODULE Stream;
IMPORT
    String;
CONST
    kCR* = 0AX;

TYPE
    Type* = POINTER TO RECORD
        s: STRING;
        pos: INTEGER
    END;

    ReaderProc = PROCEDURE(c: CHAR): BOOLEAN;

PROCEDURE make*(text: STRING): Type;
VAR result: Type;
BEGIN
    NEW(result);
    result.s := text;
    RETURN result
END make;

PROCEDURE eof*(self: Type): BOOLEAN;
    RETURN self.pos = LEN(self.s)
END eof;

PROCEDURE pos*(self: Type): INTEGER;
    RETURN self.pos
END pos;

PROCEDURE setPos*(self: Type; pos: INTEGER);
BEGIN
    ASSERT(pos <= LEN(self.s));
    self.pos := pos
END setPos;

PROCEDURE next*(self: Type; n: INTEGER);
BEGIN
    ASSERT(self.pos + n <= LEN(self.s));
    self.pos := self.pos + n;
END next;

PROCEDURE peekChar*(self: Type): CHAR;
BEGIN
    ASSERT(~eof(self));
    RETURN self.s[self.pos]
END peekChar;

PROCEDURE getChar*(self: Type): CHAR;
VAR result: CHAR;
BEGIN
    ASSERT(~eof(self));
    result := self.s[self.pos];
    INC(self.pos);
    RETURN result
END getChar;

PROCEDURE peekStr*(self: Type; s: STRING): BOOLEAN;
VAR
    result: BOOLEAN;
    i: INTEGER;
BEGIN
    IF LEN(s) <= LEN(self.s) - self.pos THEN
        WHILE (i < LEN(s)) & (s[i] = self.s[self.pos + i]) DO
            INC(i)
        END;
        result := i = LEN(s);
    END
    RETURN result
END peekStr;

PROCEDURE read*(self: Type; f: ReaderProc): BOOLEAN;
BEGIN
    WHILE ~eof(self) & f(peekChar(self)) DO
        next(self, 1);
    END
    RETURN ~eof(self)
END read;

PROCEDURE lineNumber*(self: Type): INTEGER;
VAR 
    line: INTEGER;
    lastPos: INTEGER;
BEGIN
    lastPos := String.indexOf(self.s, kCR);
    WHILE (lastPos # -1) & (lastPos < self.pos) DO
        INC(line);
        lastPos := String.indexOfFrom(self.s, kCR, lastPos + 1);
    END;
    RETURN line + 1
END lineNumber;

END Stream.