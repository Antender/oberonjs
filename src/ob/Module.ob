MODULE Module;
IMPORT Code, Context, Errors, JsArray, JsString, Procedure, Symbols, Types;
TYPE
    Type* = RECORD(Types.Module)
        PROCEDURE findSymbol(id: JsString.Type): Symbols.PFoundSymbol
    END;
    PType* = POINTER TO Type;

    AnyType* = RECORD(Types.StorageType)
        PROCEDURE callGenerator(cx: Context.PType; id: JsString.Type): Procedure.PCallGenerator;
        PROCEDURE findSymbol(id: JsString.Type): Types.PType
    END;

    AnyTypeProc* = RECORD(Types.DefinedProcedure)
    END;

    JS = RECORD(Type)
    END;
VAR
    doProcId, varTypeId: JsString.Type;
    any: POINTER TO AnyType;
    anyProc: AnyTypeProc;
    doProcSymbol, varTypeSymbol: Symbols.PSymbol;

PROCEDURE AnyType.description(): JsString.Type;
    RETURN JsString.make("JS.var")
END AnyType.description;

PROCEDURE AnyType.initializer(cx: Context.Type): JsString.Type;
    RETURN JsString.make("undefined")
END AnyType.initializer;

PROCEDURE AnyType.callGenerator(cx: Context.PType; id: JsString.Type): Procedure.PCallGenerator;
    RETURN Procedure.makeProcCallGenerator(cx, id, anyProc)
END AnyType.callGenerator;

PROCEDURE AnyType.findSymbol(id: JsString.Type): Types.PType;
    RETURN any
END AnyType.findSymbol;

PROCEDURE AnyTypeProc.args(): JsArray.Type;
    RETURN NIL
END AnyTypeProc.args;

PROCEDURE AnyTypeProc.result(): Types.PType;
    RETURN any
END AnyTypeProc.result;

PROCEDURE JS.findSymbol(id: JsString.Type): Symbols.PFoundSymbol;
VAR
    result: Symbols.PSymbol;
BEGIN
    IF JsString.eq(id, doProcId) THEN
        result := doProcSymbol;
    ELSIF JsString.eq(id, varTypeId) THEN
        result := varTypeSymbol;
    ELSE
        result := Symbols.makeSymbol(id, Types.makeProcedure(any));
    END;
    RETURN Symbols.makeFound(result, NIL)
END JS.findSymbol;

PROCEDURE makeVarTypeSymbol(): Symbols.PSymbol;
    RETURN Symbols.makeSymbol(varTypeId, Types.makeTypeId(any))
END makeVarTypeSymbol;

PROCEDURE makeDoProcSymbol(): Symbols.PSymbol;
TYPE
    Call = RECORD(Procedure.StdCall)
    END;
    Proc = RECORD(Procedure.Std)
    END;
VAR
    description: JsString.Type;
    call: POINTER TO Call;
    proc: POINTER TO Proc;

    PROCEDURE Call.make(args: JsArray.Type; cx: Context.Type): Code.PExpression;
    VAR
        arg: Code.PExpression;
        type: Types.PType;
    BEGIN
        arg := Procedure.checkSingleArgument(args, SELF);
        type := arg.type();
        IF ~(type IS Types.PString) THEN
            Errors.raise(JsString.concat(JsString.concat(JsString.concat(
                JsString.make("string is expected as an argument of "),
                description),
                JsString.make(", got ")),
                type.description()));
        END;
        RETURN Code.makeSimpleExpression(Types.stringValue(type(Types.PString)^), NIL)
    END Call.make;

    PROCEDURE Proc.description(): JsString.Type;
        RETURN description
    END Proc.description;
BEGIN
    description := JsString.make("JS predefined procedure 'do'");
    NEW(call);
    Procedure.initStdCall(call);
    Procedure.hasArgumentWithCustomType(call);

    NEW(proc);
    Procedure.initStd(JsString.makeEmpty(), call, proc^);
    RETURN Procedure.makeSymbol(proc)
END makeDoProcSymbol;

PROCEDURE makeJS*(): PType;
VAR
    result: POINTER TO JS;
BEGIN
    NEW(result);
    Types.initModule(result^, JsString.make("this"));
    RETURN result
END makeJS;

BEGIN
    doProcId := JsString.make("do$");
    varTypeId := JsString.make("var$");
    NEW(any);
    doProcSymbol := makeDoProcSymbol();
    varTypeSymbol := makeVarTypeSymbol();
END Module.