MODULE Code;
IMPORT 
    JsMap, 
    Errors,
    Object, 
    Stream, 
    ScopeBase, 
    Symbols, 
    Precedence := CodePrecedence, 
    String, 
    Types;
CONST
    kTab = 09X;

TYPE
    IGenerator = RECORD
        PROCEDURE write(s: STRING);
        PROCEDURE openScope();
        PROCEDURE closeScope(ending: STRING);
        PROCEDURE result(): STRING
    END;

    PIGenerator = POINTER TO IGenerator;

    NullGenerator = RECORD(IGenerator)
    END;

    SimpleGenerator = RECORD(NullGenerator)
        mResult: STRING
    END;

    Generator = RECORD(SimpleGenerator)
        indent: INTEGER
    END;

    RefCodeProc = PROCEDURE(s: STRING): STRING;

    Designator* = RECORD
        PROCEDURE code(): STRING;
        PROCEDURE lval(): STRING;
        PROCEDURE refCode(): RefCodeProc;
        PROCEDURE type(): Types.PType;
        PROCEDURE info*(): Types.PId;
        PROCEDURE scope(): ScopeBase.PType;

        mCode: STRING;
        mLval: STRING;
        mRefCode: RefCodeProc;
        mType: Types.PType;
        mInfo: Types.PId;
        mScope: ScopeBase.PType
    END;

    PDesignator* = POINTER TO Designator;

    Const* = RECORD
    END;
    PConst* = POINTER TO Const;

    IntConst* = RECORD (Const)
        value*: INTEGER
    END;

    RealConst* = RECORD (Const)
        value*: REAL
    END;

    SetConst* = RECORD (Const)
        value*: SET
    END;

    StringConst* = RECORD (Const)
        value*: STRING
    END;

    Expression* = RECORD(Object.Type)
        PROCEDURE code*(): STRING;
        PROCEDURE lval*(): STRING;
        PROCEDURE type*(): Types.PType;
        PROCEDURE designator*(): PDesignator;
        PROCEDURE constValue*(): PConst;
        PROCEDURE maxPrecedence*(): INTEGER;
        PROCEDURE isTerm*(): BOOLEAN;

        mCode: STRING;
        mType: Types.PType;
        mDesignator: PDesignator;
        mConstValue: PConst;
        mMaxPrecedence: INTEGER
    END;

    PExpression* = POINTER TO Expression;

    ModuleGenerator = RECORD
        PROCEDURE prolog(): STRING;
        PROCEDURE epilog(exports: JsMap.Type): STRING;

        name: STRING;
        imports: JsMap.Strings
    END;

    PModuleGenerator = POINTER TO ModuleGenerator;

    Closure = RECORD(Object.Type)
        result: STRING
    END;

VAR
    nullGenerator*: NullGenerator;

PROCEDURE NullGenerator.write(s: STRING);
END NullGenerator.write;

PROCEDURE NullGenerator.openScope();
END NullGenerator.openScope;

PROCEDURE NullGenerator.closeScope(ending: STRING);
END NullGenerator.closeScope;

PROCEDURE NullGenerator.result(): STRING;
    RETURN ""
END NullGenerator.result;

PROCEDURE SimpleGenerator.write(s: STRING);
BEGIN
    SELF.mResult := SELF.mResult + s;
END SimpleGenerator.write;

PROCEDURE SimpleGenerator.result(): STRING;
    RETURN SELF.mResult
END SimpleGenerator.result;

PROCEDURE putIndent(s: STRING; indent: INTEGER): STRING;
BEGIN
    result <- s;
    FOR i <- 0 TO indent - 1 DO
        result := result + kTab;
    END;
    RETURN result
END putIndent;

PROCEDURE Generator.write(s: STRING);
VAR
    pos: INTEGER;
    index: INTEGER;
BEGIN
    index := String.indexOf(s, Stream.kCR);
    WHILE index # -1 DO
        INC(index);
        SELF.mResult := SELF.mResult + String.substr(s, pos, index - pos);
        SELF.mResult := putIndent(SELF.mResult, SELF.indent);
        pos := index;
        index := String.indexOfFrom(s, Stream.kCR, pos);
    END;
    SELF.mResult := SELF.mResult + String.substr(s, pos, LEN(s) - pos);
END Generator.write;

PROCEDURE Generator.openScope();
BEGIN
    INC(SELF.indent);
    SELF.mResult := SELF.mResult + "{" + Stream.kCR;
    SELF.mResult := putIndent(SELF.mResult, SELF.indent);
END Generator.openScope;

PROCEDURE Generator.closeScope(ending: STRING);
BEGIN
    DEC(SELF.indent);
    SELF.mResult := String.substr(SELF.mResult, 0, LEN(SELF.mResult) - 1) + "}";
    IF LEN(ending) # 0 THEN
        SELF.write(ending);
    ELSE
        SELF.mResult := SELF.mResult + Stream.kCR;
        SELF.mResult := putIndent(SELF.mResult, SELF.indent);
    END;
END Generator.closeScope;

PROCEDURE Expression.code(): STRING;
    RETURN SELF.mCode
END Expression.code;

PROCEDURE Expression.lval(): STRING;
VAR
    result: STRING;
BEGIN
    IF SELF.mDesignator # NIL THEN
        result := SELF.mDesignator.mLval;
    ELSE
        result := SELF.mCode;
    END;
    RETURN result
END Expression.lval;

PROCEDURE Expression.type(): Types.PType;
    RETURN SELF.mType
END Expression.type;

PROCEDURE Expression.designator(): PDesignator;
    RETURN SELF.mDesignator
END Expression.designator;

PROCEDURE Expression.constValue(): PConst;
    RETURN SELF.mConstValue
END Expression.constValue;

PROCEDURE Expression.maxPrecedence(): INTEGER;
    RETURN SELF.mMaxPrecedence
END Expression.maxPrecedence;

PROCEDURE Expression.isTerm(): BOOLEAN;
    RETURN (SELF.mDesignator = NIL) & (SELF.mMaxPrecedence = Precedence.none)
END Expression.isTerm;

PROCEDURE makeIntConst*(n: INTEGER): PConst;
VAR
    result: POINTER TO IntConst;
BEGIN
    NEW(result);
    result.value := n;
    RETURN result
END makeIntConst;

PROCEDURE makeRealConst*(n: REAL): PConst;
VAR
    result: POINTER TO RealConst;
BEGIN
    NEW(result);
    result.value := n;
    RETURN result
END makeRealConst;

PROCEDURE makeSetConst*(s: SET): PConst;
VAR
    result: POINTER TO SetConst;
BEGIN
    NEW(result);
    result.value := s;
    RETURN result
END makeSetConst;

PROCEDURE makeStringConst*(s: STRING): PConst;
VAR
    result: POINTER TO StringConst;
BEGIN
    NEW(result);
    result.value := s;
    RETURN result
END makeStringConst;

PROCEDURE makeExpressionWithPrecedence*(
    code: STRING; 
    type: Types.PType; 
    designator: PDesignator; 
    constValue: PConst; 
    maxPrecedence: INTEGER): PExpression;
VAR
    result: PExpression;
BEGIN
    NEW(result);
    result.mCode := code;
    result.mType := type;
    result.mDesignator := designator;
    result.mConstValue := constValue;
    result.mMaxPrecedence := maxPrecedence;
    RETURN result
END makeExpressionWithPrecedence;

PROCEDURE makeExpression*(
    code: STRING; 
    type: Types.PType; 
    designator: PDesignator; 
    constValue: PConst)
    : PExpression;
    RETURN makeExpressionWithPrecedence(code, type, designator, constValue, Precedence.none)
END makeExpression;

PROCEDURE makeSimpleExpression*(
    code: STRING; 
    type: Types.PType)
    : PExpression;
    RETURN makeExpression(code, type, NIL, NIL)
END makeSimpleExpression;

PROCEDURE Designator.code(): STRING;
    RETURN SELF.mCode
END Designator.code;

PROCEDURE Designator.lval(): STRING;
    RETURN SELF.mLval
END Designator.lval;

PROCEDURE Designator.refCode(): RefCodeProc;
    RETURN SELF.mRefCode
END Designator.refCode;

PROCEDURE Designator.type(): Types.PType;
    RETURN SELF.mType
END Designator.type;

PROCEDURE Designator.info(): Types.PId;
    RETURN SELF.mInfo
END Designator.info;

PROCEDURE Designator.scope(): ScopeBase.PType;
    RETURN SELF.mScope
END Designator.scope;

PROCEDURE makeDesignator*(code: STRING; lval: STRING; refCode: RefCodeProc; type: Types.PType; info: Types.PId; scope: ScopeBase.PType): PDesignator;
VAR
    result: PDesignator;
BEGIN
    NEW(result);
    result.mCode := code;
    result.mLval := lval;
    result.mRefCode := refCode;
    result.mType := type;
    result.mInfo := info;
    result.mScope := scope;
    RETURN result
END makeDesignator;

PROCEDURE derefExpression*(e: PExpression): PExpression;
VAR
    result: PExpression;
BEGIN
    IF     (e.mDesignator = NIL)
        OR ((e.mType IS Types.PArray) OR (e.mType IS Types.PRecord)) 
        OR ~((e.mDesignator.mInfo IS Types.PVariable) 
                & e.mDesignator.mInfo(Types.PVariable).isReference()) THEN
        result := e;
    ELSE
        result := makeSimpleExpression(e.mCode + ".get()", e.mType);
    END;
    RETURN result
END derefExpression;

PROCEDURE refExpression*(e: PExpression): PExpression;
VAR
    result: PExpression;
BEGIN
    IF     (e.mDesignator = NIL) 
        OR ((e.mDesignator.mInfo IS Types.PVariable) 
            & e.mDesignator.mInfo(Types.PVariable).isReference()) THEN
        result := e;
    ELSE
        result := makeSimpleExpression(e.mDesignator.mRefCode(e.mDesignator.mCode),
                                       e.mType);
    END;
    RETURN result
END refExpression;

PROCEDURE adjustPrecedence*(e: PExpression; precedence: INTEGER): STRING;
VAR
    result: STRING;
BEGIN
    result := e.mCode;
    IF (precedence # Precedence.none) & (e.mMaxPrecedence > precedence) THEN
        result := "(" + result + ")";
    END;
    RETURN result
END adjustPrecedence;

PROCEDURE isPointerShouldBeExported(type: Types.Pointer): STRING;
VAR
    r: Types.PRecord;
    result: STRING;
BEGIN
    r := Types.pointerBase(type);
    IF LEN(Types.typeName(r^)) = 0 THEN
        result := Types.recordConstructor(r^);
    END;
    RETURN result
END isPointerShouldBeExported;

PROCEDURE typeShouldBeExported(typeId: Types.PId; defaultId: STRING): STRING;
VAR
    result: STRING;
    type: Types.PType;
BEGIN
    type := typeId(Types.PTypeId).type();
    IF type IS Types.PRecord THEN
        result := defaultId;
    ELSIF type IS Types.PPointer THEN
        result := isPointerShouldBeExported(type^(Types.Pointer));
    END;
    RETURN result
END typeShouldBeExported;

PROCEDURE genExport*(s: Symbols.Symbol): STRING;
VAR
    result: STRING;
BEGIN
    IF s.isVariable() THEN
        result := "function(){return " + s.id() + ";}";
    ELSIF ~s.isType() THEN
        result := s.id();
    ELSE
        result := typeShouldBeExported(s.info(), s.id())
    END;
    RETURN result
END genExport;

PROCEDURE genCommaList(name: STRING; VAR closure: Closure);
BEGIN
    IF LEN(closure.result) # 0 THEN
        closure.result := closure.result + ", ";
    END;
    closure.result := closure.result + name;
END genCommaList;

PROCEDURE genAliasesAdaptor(key: STRING; value: STRING; VAR closure: Object.Type);
BEGIN
    genCommaList(value, closure(Closure));
END genAliasesAdaptor;

PROCEDURE ModuleGenerator.prolog(): STRING;
VAR
    closure: Closure;
BEGIN
    JsMap.forEachString(SELF.imports, genAliasesAdaptor, closure);
    RETURN "var " + SELF.name + " = function (" + closure.result + "){" + Stream.kCR
END ModuleGenerator.prolog;

PROCEDURE genExports(s: Symbols.Symbol; VAR closure: Closure);
VAR
    code: STRING;
BEGIN
    code := genExport(s);
    IF LEN(code) # 0 THEN
        IF LEN(closure.result) # 0 THEN
            closure.result := closure.result + "," + Stream.kCR;
        END;
        closure.result := closure.result + kTab + s.id() + ": " + code;
    END;
END genExports;

PROCEDURE genExportsAdaptor(key: STRING; value: Object.PType; VAR closure: Object.Type);
BEGIN
    genExports(value^(Symbols.Symbol), closure(Closure));
END genExportsAdaptor;

PROCEDURE genImportListAdaptor(key: STRING; value: STRING; VAR closure: Object.Type);
BEGIN
    genCommaList(key, closure(Closure));
END genImportListAdaptor;

PROCEDURE ModuleGenerator.epilog(exports: JsMap.Type): STRING;
VAR
    result: STRING;
    closure: Closure;
BEGIN
    JsMap.forEach(exports, genExportsAdaptor, closure);
    result := closure.result;
    IF LEN(result) # 0 THEN
        result := "return {" + Stream.kCR + result + Stream.kCR + "}" + Stream.kCR;
    END;
    result := result + "}(";

    closure.result := "";
    JsMap.forEachString(SELF.imports, genImportListAdaptor, closure);
    result := result + closure.result + ");" + Stream.kCR;
    RETURN result
END ModuleGenerator.epilog;

PROCEDURE makeSimpleGenerator*(): PIGenerator;
VAR
    result: POINTER TO SimpleGenerator;
BEGIN
    NEW(result);
    RETURN result
END makeSimpleGenerator;

PROCEDURE makeGenerator*(): PIGenerator;
VAR
    result: POINTER TO Generator;
BEGIN
    NEW(result);
    RETURN result
END makeGenerator;

PROCEDURE makeModuleGenerator*(name: STRING; imports: JsMap.Strings): PModuleGenerator;
VAR
    result: PModuleGenerator;
BEGIN
    NEW(result);
    result.name := name;
    result.imports := imports;
    RETURN result
END makeModuleGenerator;

PROCEDURE checkIndex*(i: INTEGER);
BEGIN
    IF i < 0 THEN
        Errors.raise("index is negative: " + String.fromInt(i));
    END;
END checkIndex;

END Code.