MODULE Code;
IMPORT 
    CodeGenerator,
    JsMap, 
    Errors,
    Object, 
    Stream, 
    ScopeBase, 
    Symbols, 
    Precedence := CodePrecedence, 
    String, 
    Types;

TYPE
    RefCodeProc = PROCEDURE(s: STRING): STRING;

    Designator* = RECORD
        PROCEDURE Designator*(code: STRING; lval: STRING; refCode: RefCodeProc; type: Types.PType; info: Types.PId; scope: ScopeBase.PType);

        PROCEDURE code(): STRING;
        PROCEDURE lval(): STRING;
        PROCEDURE refCode(): RefCodeProc;
        PROCEDURE type(): Types.PType;
        PROCEDURE info*(): Types.PId;
        PROCEDURE scope(): ScopeBase.PType;

        mCode: STRING;
        mLval: STRING;
        mRefCode: RefCodeProc;
        mType: Types.PType;
        mInfo: Types.PId;
        mScope: ScopeBase.PType
    END;

    PDesignator* = POINTER TO Designator;

    Const* = RECORD
    END;
    PConst* = POINTER TO Const;

    IntConst* = RECORD (Const)
        value*: INTEGER
    END;

    RealConst* = RECORD (Const)
        value*: REAL
    END;

    SetConst* = RECORD (Const)
        value*: SET
    END;

    StringConst* = RECORD (Const)
        value*: STRING
    END;

    Expression* = RECORD(Object.Type)
        PROCEDURE Expression*(
            code: STRING; 
            type: Types.PType; 
            designator: PDesignator; 
            constValue: PConst; 
            maxPrecedence: INTEGER);

        PROCEDURE code*(): STRING;
        PROCEDURE lval*(): STRING;
        PROCEDURE type*(): Types.PType;
        PROCEDURE designator*(): PDesignator;
        PROCEDURE constValue*(): PConst;
        PROCEDURE maxPrecedence*(): INTEGER;
        PROCEDURE isTerm*(): BOOLEAN;

        mCode: STRING;
        mType: Types.PType;
        mDesignator: PDesignator;
        mConstValue: PConst;
        mMaxPrecedence: INTEGER
    END;

    PExpression* = POINTER TO Expression;

    ModuleGenerator* = RECORD
        PROCEDURE ModuleGenerator*(name: STRING; imports: JsMap.Strings);

        PROCEDURE prolog(): STRING;
        PROCEDURE epilog(exports: JsMap.Type): STRING;

        name: STRING;
        imports: JsMap.Strings
    END;

    PModuleGenerator = POINTER TO ModuleGenerator;

    Closure = RECORD(Object.Type)
        result: STRING
    END;

PROCEDURE Expression.code(): STRING;
    RETURN SELF.mCode
END Expression.code;

PROCEDURE Expression.lval(): STRING;
VAR
    result: STRING;
BEGIN
    IF SELF.mDesignator # NIL THEN
        result := SELF.mDesignator.mLval;
    ELSE
        result := SELF.mCode;
    END;
    RETURN result
END Expression.lval;

PROCEDURE Expression.type(): Types.PType;
    RETURN SELF.mType
END Expression.type;

PROCEDURE Expression.designator(): PDesignator;
    RETURN SELF.mDesignator
END Expression.designator;

PROCEDURE Expression.constValue(): PConst;
    RETURN SELF.mConstValue
END Expression.constValue;

PROCEDURE Expression.maxPrecedence(): INTEGER;
    RETURN SELF.mMaxPrecedence
END Expression.maxPrecedence;

PROCEDURE Expression.isTerm(): BOOLEAN;
    RETURN (SELF.mDesignator = NIL) & (SELF.mMaxPrecedence = Precedence.none)
END Expression.isTerm;

PROCEDURE makeIntConst*(n: INTEGER): PConst;
BEGIN
    result <- NEW IntConst();
    result.value := n;
    RETURN result
END;

PROCEDURE makeRealConst*(n: REAL): PConst;
BEGIN
    result <- NEW RealConst();
    result.value := n;
    RETURN result
END;

PROCEDURE makeSetConst*(s: SET): PConst;
BEGIN
    result <- NEW SetConst();
    result.value := s;
    RETURN result
END;

PROCEDURE makeStringConst*(s: STRING): PConst;
BEGIN
    result <- NEW StringConst();
    result.value := s;
    RETURN result
END;

PROCEDURE Expression.Expression(
    code: STRING; 
    type: Types.PType; 
    designator: PDesignator; 
    constValue: PConst; 
    maxPrecedence: INTEGER)
  | mCode(code),
    mType(type),
    mDesignator(designator),
    mConstValue(constValue),
    mMaxPrecedence(maxPrecedence);
END;

PROCEDURE makeExpression*(
    code: STRING; 
    type: Types.PType; 
    designator: PDesignator; 
    constValue: PConst)
    : PExpression;
    RETURN NEW Expression(code, type, designator, constValue, Precedence.none)
END makeExpression;

PROCEDURE makeSimpleExpression*(
    code: STRING; 
    type: Types.PType)
    : PExpression;
    RETURN makeExpression(code, type, NIL, NIL)
END makeSimpleExpression;

PROCEDURE Designator.code(): STRING;
    RETURN SELF.mCode
END Designator.code;

PROCEDURE Designator.lval(): STRING;
    RETURN SELF.mLval
END Designator.lval;

PROCEDURE Designator.refCode(): RefCodeProc;
    RETURN SELF.mRefCode
END Designator.refCode;

PROCEDURE Designator.type(): Types.PType;
    RETURN SELF.mType
END Designator.type;

PROCEDURE Designator.info(): Types.PId;
    RETURN SELF.mInfo
END Designator.info;

PROCEDURE Designator.scope(): ScopeBase.PType;
    RETURN SELF.mScope
END Designator.scope;

PROCEDURE Designator.Designator(code: STRING; lval: STRING; refCode: RefCodeProc; type: Types.PType; info: Types.PId; scope: ScopeBase.PType)
  | mCode(code),
    mLval(lval),
    mRefCode(refCode),
    mType(type),
    mInfo(info),
    mScope(scope);
END;

PROCEDURE derefExpression*(e: PExpression): PExpression;
VAR
    result: PExpression;
BEGIN
    IF     (e.mDesignator = NIL)
        OR ((e.mType IS Types.PArray) OR (e.mType IS Types.PRecord)) 
        OR ~((e.mDesignator.mInfo IS Types.PVariable) 
                & e.mDesignator.mInfo(Types.PVariable).isReference()) THEN
        result := e;
    ELSE
        result := makeSimpleExpression(e.mCode + ".get()", e.mType);
    END;
    RETURN result
END derefExpression;

PROCEDURE refExpression*(e: PExpression): PExpression;
VAR
    result: PExpression;
BEGIN
    IF     (e.mDesignator = NIL) 
        OR ((e.mDesignator.mInfo IS Types.PVariable) 
            & e.mDesignator.mInfo(Types.PVariable).isReference()) THEN
        result := e;
    ELSE
        result := makeSimpleExpression(e.mDesignator.mRefCode(e.mDesignator.mCode),
                                       e.mType);
    END;
    RETURN result
END refExpression;

PROCEDURE adjustPrecedence*(e: PExpression; precedence: INTEGER): STRING;
VAR
    result: STRING;
BEGIN
    result := e.mCode;
    IF (precedence # Precedence.none) & (e.mMaxPrecedence > precedence) THEN
        result := "(" + result + ")";
    END;
    RETURN result
END adjustPrecedence;

PROCEDURE isPointerShouldBeExported(type: Types.Pointer): STRING;
VAR
    r: Types.PRecord;
    result: STRING;
BEGIN
    r := Types.pointerBase(type);
    IF LEN(Types.typeName(r^)) = 0 THEN
        result := Types.recordConstructor(r^);
    END;
    RETURN result
END isPointerShouldBeExported;

PROCEDURE typeShouldBeExported(typeId: Types.PId; defaultId: STRING): STRING;
VAR
    result: STRING;
    type: Types.PType;
BEGIN
    type := typeId(Types.PTypeId).type();
    IF type IS Types.PRecord THEN
        result := defaultId;
    ELSIF type IS Types.PPointer THEN
        result := isPointerShouldBeExported(type^(Types.Pointer));
    END;
    RETURN result
END typeShouldBeExported;

PROCEDURE genExport*(s: Symbols.Symbol): STRING;
VAR
    result: STRING;
BEGIN
    IF s.isVariable() THEN
        result := "function(){return " + s.id() + ";}";
    ELSIF ~s.isType() THEN
        result := s.id();
    ELSE
        result := typeShouldBeExported(s.info(), s.id())
    END;
    RETURN result
END genExport;

PROCEDURE genCommaList(name: STRING; VAR closure: Closure);
BEGIN
    IF LEN(closure.result) # 0 THEN
        closure.result := closure.result + ", ";
    END;
    closure.result := closure.result + name;
END genCommaList;

PROCEDURE genAliasesAdaptor(key: STRING; value: STRING; VAR closure: Object.Type);
BEGIN
    genCommaList(value, closure(Closure));
END genAliasesAdaptor;

PROCEDURE ModuleGenerator.prolog(): STRING;
VAR
    closure: Closure;
BEGIN
    JsMap.forEachString(SELF.imports, genAliasesAdaptor, closure);
    RETURN "var " + SELF.name + " = function (" + closure.result + "){" + Stream.kCR
END ModuleGenerator.prolog;

PROCEDURE genExports(s: Symbols.Symbol; VAR closure: Closure);
VAR
    code: STRING;
BEGIN
    code := genExport(s);
    IF LEN(code) # 0 THEN
        IF LEN(closure.result) # 0 THEN
            closure.result := closure.result + "," + Stream.kCR;
        END;
        closure.result := closure.result + CodeGenerator.kTab + s.id() + ": " + code;
    END;
END genExports;

PROCEDURE genExportsAdaptor(key: STRING; value: Object.PType; VAR closure: Object.Type);
BEGIN
    genExports(value^(Symbols.Symbol), closure(Closure));
END genExportsAdaptor;

PROCEDURE genImportListAdaptor(key: STRING; value: STRING; VAR closure: Object.Type);
BEGIN
    genCommaList(key, closure(Closure));
END genImportListAdaptor;

PROCEDURE ModuleGenerator.epilog(exports: JsMap.Type): STRING;
VAR
    result: STRING;
    closure: Closure;
BEGIN
    JsMap.forEach(exports, genExportsAdaptor, closure);
    result := closure.result;
    IF LEN(result) # 0 THEN
        result := "return {" + Stream.kCR + result + Stream.kCR + "}" + Stream.kCR;
    END;
    result := result + "}(";

    closure.result := "";
    JsMap.forEachString(SELF.imports, genImportListAdaptor, closure);
    result := result + closure.result + ");" + Stream.kCR;
    RETURN result
END ModuleGenerator.epilog;

PROCEDURE ModuleGenerator.ModuleGenerator(name: STRING; imports: JsMap.Strings)
    | name(name),
      imports(imports);
END;

PROCEDURE checkIndex*(i: INTEGER);
BEGIN
    IF i < 0 THEN
        Errors.raise("index is negative: " + String.fromInt(i));
    END;
END checkIndex;

END Code.