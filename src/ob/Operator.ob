MODULE Operator;
IMPORT 
    Cast, 
    Code, 
    Errors, 
    Language, 
    LanguageContext, 
    OberonRtl, 
    Precedence := CodePrecedence,
    String,
    Types;
TYPE
    BinaryProc = PROCEDURE(left, right: Code.PExpression; rtl: OberonRtl.PType): Code.PExpression;

    BinaryOp = PROCEDURE(left, right: Code.PConst): Code.PConst;
    CodePredicate = PROCEDURE(left, right: STRING; rtl: OberonRtl.PType): STRING;

    UnaryOp = PROCEDURE(value: Code.PConst): Code.PConst;

    CodeMaker = RECORD
        PROCEDURE make(left, right: STRING; rtl: OberonRtl.PType): STRING
    END;
    PCodeMaker = POINTER TO CodeMaker;

    SimpleCodeMaker = RECORD (CodeMaker)
        code: STRING
    END;

    IntCodeMaker = RECORD (SimpleCodeMaker)
    END;

    PredCodeMaker = RECORD (CodeMaker)
        pred: CodePredicate
    END;

    CastToUint8 = RECORD (Cast.CastOp)
    END;

VAR
    openArrayChar: Types.PType;
    castOperations*: Cast.Operations;
    castToUint8: POINTER TO CastToUint8;

PROCEDURE binary(
    left, right: Code.PExpression; 
    rtl: OberonRtl.PType;
    op: BinaryOp;
    code: PCodeMaker;
    precedence: INTEGER;
    optResultType: Types.PType;
    optResultPrecedence: INTEGER
    ): Code.PExpression;
VAR
    result: Code.PExpression;
    leftValue, rightValue, resultValue: Code.PConst;
    leftCode, rightCode, resultCode: STRING;
    resultType: Types.PType;
    resultPrecedence: INTEGER;
    rightExpDeref: Code.PExpression;
BEGIN
    leftValue := left.constValue();
    rightValue := right.constValue();
    IF (leftValue # NIL) & (rightValue # NIL) THEN
        resultValue := op(leftValue, rightValue);
    END;

    leftCode := Code.adjustPrecedence(Code.derefExpression(left), precedence);

    (* right code needs parentheses even if it has the same percedence *)
    rightExpDeref := Code.derefExpression(right);
    IF precedence # Precedence.none THEN
        rightCode := Code.adjustPrecedence(rightExpDeref, precedence - 1);
    ELSE
        rightCode := rightExpDeref.code();
    END;

    resultCode := code.make(leftCode, rightCode, rtl);

    IF optResultType # NIL THEN
        resultType := optResultType;
    ELSE
        resultType := left.type();
    END;

    IF optResultPrecedence # Precedence.none THEN
        resultPrecedence := optResultPrecedence;
    ELSE
        resultPrecedence := precedence;
    END;
    RETURN Code.makeExpressionWithPrecedence(resultCode, resultType, NIL, resultValue, resultPrecedence)
END binary;

PROCEDURE SimpleCodeMaker.make(left, right: STRING; rtl: OberonRtl.PType): STRING;
    RETURN left + SELF.code + right
END SimpleCodeMaker.make;

PROCEDURE IntCodeMaker.make(left, right: STRING; rtl: OberonRtl.PType): STRING;
BEGIN
    RETURN SUPER(left, right, rtl) + " | 0"
END IntCodeMaker.make;

PROCEDURE PredCodeMaker.make(left, right: STRING; rtl: OberonRtl.PType): STRING;
BEGIN
    RETURN SELF.pred(left, right, rtl)
END PredCodeMaker.make;

PROCEDURE makeSimpleCodeMaker(code: STRING): PCodeMaker;
VAR
    result: POINTER TO SimpleCodeMaker;
BEGIN
    NEW(result);
    result.code := code;
    RETURN result
END makeSimpleCodeMaker;

PROCEDURE makeIntCodeMaker(code: STRING): PCodeMaker;
VAR
    result: POINTER TO IntCodeMaker;
BEGIN
    NEW(result);
    result.code := code;
    RETURN result
END makeIntCodeMaker;

PROCEDURE makePredCodeMaker(pred: CodePredicate): PCodeMaker;
VAR
    result: POINTER TO PredCodeMaker;
BEGIN
    NEW(result);
    result.pred := pred;
    RETURN result
END makePredCodeMaker;

PROCEDURE binaryWithCodeEx(
    left, right: Code.PExpression; 
    rtl: OberonRtl.PType;
    op: BinaryOp;
    code: STRING;
    precedence: INTEGER;
    optResultType: Types.PType;
    optResultPrecedence: INTEGER
    ): Code.PExpression;
    RETURN binary(
        left, 
        right, 
        rtl, 
        op, 
        makeSimpleCodeMaker(code), 
        precedence, 
        optResultType, 
        optResultPrecedence)
END binaryWithCodeEx;

PROCEDURE binaryWithCode*(
    left, right: Code.PExpression; 
    rtl: OberonRtl.PType;
    op: BinaryOp;
    code: STRING;
    precedence: INTEGER
    ): Code.PExpression;
    RETURN binaryWithCodeEx(left, right, rtl, op, code, precedence, NIL, Precedence.none)
END binaryWithCode;

PROCEDURE relational*(
    left, right: Code.PExpression; 
    rtl: OberonRtl.PType;
    op: BinaryOp;
    code: STRING
    ): Code.PExpression;
    RETURN binaryWithCodeEx(left, right, rtl, op, code, Precedence.relational, Types.basic.bool, Precedence.none)
END relational;

PROCEDURE equal*(
    left, right: Code.PExpression; 
    rtl: OberonRtl.PType;
    op: BinaryOp;
    code: STRING
    ): Code.PExpression;
    RETURN binaryWithCodeEx(left, right, rtl, op, code, Precedence.equal, Types.basic.bool, Precedence.none)
END equal;

PROCEDURE promoteToWideIfNeeded(e: Code.PExpression): Code.PExpression;
VAR
    result: Code.PExpression;
BEGIN
    IF e.type() # Types.basic.uint8 THEN
        result := e;
    ELSE
        result := Code.makeExpressionWithPrecedence(
                e.code(),
                Types.basic.integer,
                e.designator(),
                e.constValue(),
                e.maxPrecedence());
    END;
    RETURN result
END promoteToWideIfNeeded;

PROCEDURE binaryInt(
    left, right: Code.PExpression; 
    rtl: OberonRtl.PType;
    op: BinaryOp;
    code: STRING;
    precedence: INTEGER
    ): Code.PExpression;
    RETURN promoteToWideIfNeeded(binary(
        left, 
        right, 
        rtl, 
        op, 
        makeIntCodeMaker(code), 
        precedence, 
        NIL, 
        Precedence.bitOr (* see IntCodeMaker.make *)
        ))
END binaryInt;

PROCEDURE binaryPred(
    left, right: Code.PExpression; 
    rtl: OberonRtl.PType;
    op: BinaryOp;
    pred: CodePredicate
    ): Code.PExpression;
    RETURN binary(
        left, 
        right, 
        rtl, 
        op, 
        makePredCodeMaker(pred), 
        Precedence.none, 
        Types.basic.bool, 
        Precedence.none
        )
END binaryPred;

PROCEDURE unary(e: Code.PExpression; op: UnaryOp; code: STRING): Code.PExpression;
VAR
    value: Code.PConst;
BEGIN
    value := e.constValue();
    IF value # NIL THEN
        value := op(value);
    END;
    resultCode <- code 
                + Code.adjustPrecedence(Code.derefExpression(e), Precedence.unary);
    RETURN Code.makeExpression(resultCode, e.type(), NIL, value)
END unary;

PROCEDURE castToStr(e: Code.PExpression; rtl: OberonRtl.PType): STRING;
VAR
    resultExpression: Code.PExpression;
    op: Cast.PCastOp;
    ignored: INTEGER;
BEGIN
    ignored := Cast.implicit(e.type(), openArrayChar, FALSE, castOperations, op);
    IF op # NIL THEN
        resultExpression := op.make(rtl, e)
    ELSE
        resultExpression := e;
    END;
    RETURN resultExpression.code()
END castToStr;

PROCEDURE opAddReal(left, right: Code.PConst): Code.PConst;
    RETURN Code.makeRealConst(left^(Code.RealConst).value 
                            + right^(Code.RealConst).value)
END opAddReal;

PROCEDURE opAddInt(left, right: Code.PConst): Code.PConst;
    RETURN Code.makeIntConst(left^(Code.IntConst).value 
                           + right^(Code.IntConst).value)
END opAddInt;

PROCEDURE opSubReal(left, right: Code.PConst): Code.PConst;
    RETURN Code.makeRealConst(left^(Code.RealConst).value 
                            - right^(Code.RealConst).value)
END opSubReal;

PROCEDURE opSubInt(left, right: Code.PConst): Code.PConst;
    RETURN Code.makeIntConst(left^(Code.IntConst).value 
                           - right^(Code.IntConst).value)
END opSubInt;

PROCEDURE opMulReal(left, right: Code.PConst): Code.PConst;
    RETURN Code.makeRealConst(left^(Code.RealConst).value 
                            * right^(Code.RealConst).value)
END opMulReal;

PROCEDURE opMulInt(left, right: Code.PConst): Code.PConst;
    RETURN Code.makeIntConst(left^(Code.IntConst).value 
                           * right^(Code.IntConst).value)
END opMulInt;

PROCEDURE opDivReal(left, right: Code.PConst): Code.PConst;
    RETURN Code.makeRealConst(left^(Code.RealConst).value 
                            / right^(Code.RealConst).value)
END opDivReal;

PROCEDURE opDivInt(left, right: Code.PConst): Code.PConst;
    RETURN Code.makeIntConst(left^(Code.IntConst).value 
                         DIV right^(Code.IntConst).value)
END opDivInt;

PROCEDURE opMod(left, right: Code.PConst): Code.PConst;
    RETURN Code.makeIntConst(left^(Code.IntConst).value 
                         MOD right^(Code.IntConst).value)
END opMod;

PROCEDURE opSetUnion(left, right: Code.PConst): Code.PConst;
    RETURN Code.makeSetConst(left^(Code.SetConst).value 
                           + right^(Code.SetConst).value)
END opSetUnion;

PROCEDURE opSetDiff(left, right: Code.PConst): Code.PConst;
    RETURN Code.makeSetConst(left^(Code.SetConst).value 
                           - right^(Code.SetConst).value)
END opSetDiff;

PROCEDURE opSetIntersection(left, right: Code.PConst): Code.PConst;
    RETURN Code.makeSetConst(left^(Code.SetConst).value 
                           * right^(Code.SetConst).value)
END opSetIntersection;

PROCEDURE opSetSymmetricDiff(left, right: Code.PConst): Code.PConst;
    RETURN Code.makeSetConst(left^(Code.SetConst).value 
                           / right^(Code.SetConst).value)
END opSetSymmetricDiff;

PROCEDURE opSetInclL(left, right: Code.PConst): Code.PConst;
    RETURN Code.makeIntConst(ORD(left^(Code.SetConst).value 
                              <= right^(Code.SetConst).value))
END opSetInclL;

PROCEDURE opSetInclR(left, right: Code.PConst): Code.PConst;
    RETURN Code.makeIntConst(ORD(left^(Code.SetConst).value 
                              >= right^(Code.SetConst).value))
END opSetInclR;

PROCEDURE opOr(left, right: Code.PConst): Code.PConst;
    RETURN Code.makeIntConst(ORD((left^(Code.IntConst).value # 0)
                              OR (right^(Code.IntConst).value # 0)))
END opOr;

PROCEDURE opAnd(left, right: Code.PConst): Code.PConst;
    RETURN Code.makeIntConst(ORD((left^(Code.IntConst).value # 0)
                               & (right^(Code.IntConst).value # 0)))
END opAnd;

PROCEDURE opEqualInt(left, right: Code.PConst): Code.PConst;
    RETURN Code.makeIntConst(ORD(left^(Code.IntConst).value
                               = right^(Code.IntConst).value))
END opEqualInt;

PROCEDURE opEqualReal(left, right: Code.PConst): Code.PConst;
    RETURN Code.makeIntConst(ORD(left^(Code.RealConst).value
                               = right^(Code.RealConst).value))
END opEqualReal;

PROCEDURE opEqualSet(left, right: Code.PConst): Code.PConst;
    RETURN Code.makeIntConst(ORD(left^(Code.SetConst).value
                               = right^(Code.SetConst).value))
END opEqualSet;

PROCEDURE opNotEqualInt(left, right: Code.PConst): Code.PConst;
    RETURN Code.makeIntConst(ORD(left^(Code.IntConst).value
                               # right^(Code.IntConst).value))
END opNotEqualInt;

PROCEDURE opNotEqualReal(left, right: Code.PConst): Code.PConst;
    RETURN Code.makeIntConst(ORD(left^(Code.RealConst).value
                               # right^(Code.RealConst).value))
END opNotEqualReal;

PROCEDURE opNotEqualSet(left, right: Code.PConst): Code.PConst;
    RETURN Code.makeIntConst(ORD(left^(Code.SetConst).value
                               # right^(Code.SetConst).value))
END opNotEqualSet;

PROCEDURE opLessInt(left, right: Code.PConst): Code.PConst;
    RETURN Code.makeIntConst(ORD(left^(Code.IntConst).value
                               < right^(Code.IntConst).value))
END opLessInt;

PROCEDURE opLessReal(left, right: Code.PConst): Code.PConst;
    RETURN Code.makeIntConst(ORD(left^(Code.RealConst).value
                               < right^(Code.RealConst).value))
END opLessReal;

PROCEDURE opGreaterInt(left, right: Code.PConst): Code.PConst;
    RETURN Code.makeIntConst(ORD(left^(Code.IntConst).value
                               > right^(Code.IntConst).value))
END opGreaterInt;

PROCEDURE opGreaterReal(left, right: Code.PConst): Code.PConst;
    RETURN Code.makeIntConst(ORD(left^(Code.RealConst).value
                               > right^(Code.RealConst).value))
END opGreaterReal;

PROCEDURE opEqLessInt(left, right: Code.PConst): Code.PConst;
    RETURN Code.makeIntConst(ORD(left^(Code.IntConst).value
                              <= right^(Code.IntConst).value))
END opEqLessInt;

PROCEDURE opEqLessReal(left, right: Code.PConst): Code.PConst;
    RETURN Code.makeIntConst(ORD(left^(Code.RealConst).value
                              <= right^(Code.RealConst).value))
END opEqLessReal;

PROCEDURE opEqGreaterInt(left, right: Code.PConst): Code.PConst;
    RETURN Code.makeIntConst(ORD(left^(Code.IntConst).value
                              >= right^(Code.IntConst).value))
END opEqGreaterInt;

PROCEDURE opEqGreaterReal(left, right: Code.PConst): Code.PConst;
    RETURN Code.makeIntConst(ORD(left^(Code.RealConst).value
                              >= right^(Code.RealConst).value))
END opEqGreaterReal;

PROCEDURE opNot(x: Code.PConst): Code.PConst;
    RETURN Code.makeIntConst(ORD(~(x^(Code.IntConst).value # 0)))
END opNot;

PROCEDURE opNegateInt(x: Code.PConst): Code.PConst;
    RETURN Code.makeIntConst(-x^(Code.IntConst).value)
END opNegateInt;

PROCEDURE opNegateReal(x: Code.PConst): Code.PConst;
    RETURN Code.makeRealConst(-x^(Code.RealConst).value)
END opNegateReal;

PROCEDURE opUnaryPlus(x: Code.PConst): Code.PConst;
    RETURN x
END opUnaryPlus;

PROCEDURE opSetComplement(x: Code.PConst): Code.PConst;
    RETURN Code.makeSetConst(-x^(Code.SetConst).value)
END opSetComplement;

PROCEDURE opLsl(left, right: Code.PConst): Code.PConst;
    RETURN Code.makeIntConst(LSL(left^(Code.IntConst).value,
                                 right^(Code.IntConst).value))
END opLsl;

PROCEDURE opAsr(left, right: Code.PConst): Code.PConst;
    RETURN Code.makeIntConst(ASR(left^(Code.IntConst).value,
                                 right^(Code.IntConst).value))
END opAsr;

PROCEDURE opRor(left, right: Code.PConst): Code.PConst;
    RETURN Code.makeIntConst(ROR(left^(Code.IntConst).value,
                                 right^(Code.IntConst).value))
END opRor;

PROCEDURE codeSetInclL(left, right: STRING; rtl: OberonRtl.PType): STRING;
BEGIN
    RETURN rtl.setInclL(left, right)
END codeSetInclL;

PROCEDURE codeSetInclR(left, right: STRING; rtl: OberonRtl.PType): STRING;
    RETURN rtl.setInclR(left, right)
END codeSetInclR;

PROCEDURE strCmp(op: STRING; left, right: Code.PExpression; rtl: OberonRtl.PType): Code.PExpression;
BEGIN   
    RETURN Code.makeSimpleExpression(
            rtl.strCmp(castToStr(left, rtl), castToStr(right, rtl)) + op + "0",
            Types.basic.bool)
END strCmp;

PROCEDURE assign*(left, right: Code.PExpression; cx: LanguageContext.Type): STRING;
VAR
    designator: Code.PDesignator;
    info: Types.PId;
    leftCode, rightCode: STRING;
    leftType, rightType: Types.PType;
    isArray: BOOLEAN;
    castOperation: Cast.PCastOp;
    castExp: Code.PExpression;
    ignored: BOOLEAN;
    result: STRING;

    PROCEDURE assignArrayFromString(a: Types.Array; s: Types.String): STRING;
    BEGIN
        IF Types.arrayLength(a) = Types.openArrayLength THEN
            Errors.raise("string cannot be assigned to open " + a.description());
        ELSIF Types.stringLen(s) > Types.arrayLength(a) THEN
            Errors.raise(String.fromInt(Types.arrayLength(a)) 
                         + "-character ARRAY is too small for "
                         + String.fromInt(Types.stringLen(s))
                         + "-character string");
        END;
        RETURN cx.rtl.assignArrayFromString(leftCode, rightCode)
    END assignArrayFromString;
BEGIN
    designator := left.designator();
    info := designator.info();
    IF ~(info IS Types.PVariable) 
        OR info(Types.PVariable).isReadOnly() THEN
        Errors.raise("cannot assign to " + info.idType());
    END; 

    leftCode := left.lval();
    rightCode := right.code();
    leftType := left.type();
    rightType := right.type();

    isArray := leftType IS Types.PArray;
    IF isArray
        & (Types.arrayElementsType(leftType(Types.PArray)^) = Types.basic.ch)
        & (rightType IS Types.PString) THEN
        result := assignArrayFromString(leftType(Types.PArray)^, rightType(Types.PString)^);
    ELSE
        IF cx.types.implicitCast(rightType, leftType, FALSE, castOperations, castOperation)
            # Cast.errNo THEN;
            Errors.raise("type mismatch: '" + leftCode + "' is '" + leftType.description()
                         + "' and cannot be assigned to '" + rightType.description() + "' expression");
        END;
        IF isArray & (rightType IS Types.PArray) 
            & (Types.arrayLength(leftType(Types.PArray)^) = Types.openArrayLength) THEN
            Errors.raise("'" + leftCode + "' is open '" + leftType.description() + "' and cannot be assigned");
        END;
        IF isArray OR (rightType IS Types.PRecord) THEN
            result := cx.rtl.copy(rightCode, leftCode);
        ELSE
            IF castOperation # NIL THEN
                castExp := castOperation.make(cx.rtl, Code.derefExpression(right));
            ELSE
                castExp := Code.derefExpression(right);
            END;
            rightCode := castExp.code();
            IF info IS Types.PVariableRef THEN
                rightCode := ".set(" + rightCode + ")";
            ELSE
                rightCode := " = " + rightCode;
            END;
            result := leftCode + rightCode;
        END;
    END;
    RETURN result
END assign;
    
PROCEDURE inplace(left, right: Code.PExpression; cx: LanguageContext.Type; code: STRING; altOp: BinaryProc): STRING;
VAR
    designator: Code.PDesignator;
    rightExp: Code.PExpression;
    result: STRING;
BEGIN
    designator := left.designator();
    IF designator.info() IS Types.PVariableRef THEN
        result := assign(left, altOp(left, right, cx.rtl), cx);
    ELSE
        rightExp := Code.derefExpression(right);
        result := left.code() + code + rightExp.code();
    END;
    RETURN result
END inplace;

PROCEDURE addReal*(left, right: Code.PExpression; rtl: OberonRtl.PType): Code.PExpression;
    RETURN binaryWithCode(left, right, rtl, opAddReal, " + ", Precedence.addSub)
END addReal;

PROCEDURE addInt*(left, right: Code.PExpression; rtl: OberonRtl.PType): Code.PExpression;
    RETURN binaryInt(left, right, rtl, opAddInt, " + ", Precedence.addSub)
END addInt;

PROCEDURE subReal*(left, right: Code.PExpression; rtl: OberonRtl.PType): Code.PExpression;
    RETURN binaryWithCode(left, right, rtl, opSubReal, " - ", Precedence.addSub)
END subReal;

PROCEDURE subInt*(left, right: Code.PExpression; rtl: OberonRtl.PType): Code.PExpression;
    RETURN binaryInt(left, right, rtl, opSubInt, " - ", Precedence.addSub)
END subInt;

PROCEDURE mulReal*(left, right: Code.PExpression; rtl: OberonRtl.PType): Code.PExpression;
    RETURN binaryWithCode(left, right, rtl, opMulReal, " * ", Precedence.mulDivMod)
END mulReal;

PROCEDURE mulInt*(left, right: Code.PExpression; rtl: OberonRtl.PType): Code.PExpression;
    RETURN binaryInt(left, right, rtl, opMulInt, " * ", Precedence.mulDivMod)
END mulInt;

PROCEDURE divReal*(left, right: Code.PExpression; rtl: OberonRtl.PType): Code.PExpression;
    RETURN binaryWithCode(left, right, rtl, opDivReal, " / ", Precedence.mulDivMod)
END divReal;

PROCEDURE divInt*(left, right: Code.PExpression; rtl: OberonRtl.PType): Code.PExpression;
    RETURN binaryInt(left, right, rtl, opDivInt, " / ", Precedence.mulDivMod)
END divInt;

PROCEDURE mod*(left, right: Code.PExpression; rtl: OberonRtl.PType): Code.PExpression;
    RETURN binaryWithCode(left, right, rtl, opMod, " % ", Precedence.mulDivMod)
END mod;

PROCEDURE setUnion*(left, right: Code.PExpression; rtl: OberonRtl.PType): Code.PExpression;
    RETURN binaryWithCode(left, right, rtl, opSetUnion, " | ", Precedence.bitOr)
END setUnion;

PROCEDURE setDiff*(left, right: Code.PExpression; rtl: OberonRtl.PType): Code.PExpression;
    RETURN binaryWithCode(left, right, rtl, opSetDiff, " & ~", Precedence.bitAnd)
END setDiff;

PROCEDURE setIntersection*(left, right: Code.PExpression; rtl: OberonRtl.PType): Code.PExpression;
    RETURN binaryWithCode(left, right, rtl, opSetIntersection, " & ", Precedence.bitAnd)
END setIntersection;

PROCEDURE setSymmetricDiff*(left, right: Code.PExpression; rtl: OberonRtl.PType): Code.PExpression;
    RETURN binaryWithCode(left, right, rtl, opSetSymmetricDiff, " ^ ", Precedence.bitXor)
END setSymmetricDiff;

PROCEDURE setHasBit*(left, right: Code.PExpression; rtl: OberonRtl.PType): Code.PExpression;
    RETURN Code.makeExpressionWithPrecedence(
            "1 << " + Code.adjustPrecedence(Code.derefExpression(left), Precedence.shift) 
            + " & " + Code.adjustPrecedence(Code.derefExpression(right), Precedence.bitAnd),
            Types.basic.bool,
            NIL,
            NIL,
            Precedence.bitAnd)
END setHasBit;

PROCEDURE setInclL*(left, right: Code.PExpression; rtl: OberonRtl.PType): Code.PExpression;
    RETURN binaryPred(left, right, rtl, opSetInclL, codeSetInclL)
END setInclL;

PROCEDURE setInclR*(left, right: Code.PExpression; rtl: OberonRtl.PType): Code.PExpression;
    RETURN binaryPred(left, right, rtl, opSetInclR, codeSetInclR)
END setInclR;

PROCEDURE or*(left, right: Code.PExpression; rtl: OberonRtl.PType): Code.PExpression;
    RETURN binaryWithCode(left, right, rtl, opOr, " || ", Precedence.or)
END or;

PROCEDURE and*(left, right: Code.PExpression; rtl: OberonRtl.PType): Code.PExpression;
    RETURN binaryWithCode(left, right, rtl, opAnd, " && ", Precedence.and)
END and;

PROCEDURE equalInt*(left, right: Code.PExpression; rtl: OberonRtl.PType): Code.PExpression;
    RETURN equal(left, right, rtl, opEqualInt, " == ")
END equalInt;

PROCEDURE equalReal*(left, right: Code.PExpression; rtl: OberonRtl.PType): Code.PExpression;
    RETURN equal(left, right, rtl, opEqualReal, " == ")
END equalReal;

PROCEDURE equalSet*(left, right: Code.PExpression; rtl: OberonRtl.PType): Code.PExpression;
    RETURN equal(left, right, rtl, opEqualSet, " == ")
END equalSet;

PROCEDURE equalStr*(left, right: Code.PExpression; rtl: OberonRtl.PType): Code.PExpression;
    RETURN strCmp(" == ", left, right, rtl)
END equalStr;

PROCEDURE notEqualInt*(left, right: Code.PExpression; rtl: OberonRtl.PType): Code.PExpression;
    RETURN equal(left, right, rtl, opNotEqualInt, " != ")
END notEqualInt;

PROCEDURE notEqualReal*(left, right: Code.PExpression; rtl: OberonRtl.PType): Code.PExpression;
    RETURN equal(left, right, rtl, opNotEqualReal, " != ")
END notEqualReal;

PROCEDURE notEqualSet*(left, right: Code.PExpression; rtl: OberonRtl.PType): Code.PExpression;
    RETURN equal(left, right, rtl, opNotEqualSet, " != ")
END notEqualSet;

PROCEDURE notEqualStr*(left, right: Code.PExpression; rtl: OberonRtl.PType): Code.PExpression;
    RETURN strCmp(" != ", left, right, rtl)
END notEqualStr;

PROCEDURE is*(left, right: Code.PExpression; rtl: OberonRtl.PType): Code.PExpression;
    RETURN relational(left, right, rtl, NIL, " instanceof ")
END is;

PROCEDURE lessInt*(left, right: Code.PExpression; rtl: OberonRtl.PType): Code.PExpression;
    RETURN relational(left, right, rtl, opLessInt, " < ")
END lessInt;

PROCEDURE lessReal*(left, right: Code.PExpression; rtl: OberonRtl.PType): Code.PExpression;
    RETURN relational(left, right, rtl, opLessReal, " < ")
END lessReal;

PROCEDURE lessStr*(left, right: Code.PExpression; rtl: OberonRtl.PType): Code.PExpression;
    RETURN strCmp(" < ", left, right, rtl)
END lessStr;

PROCEDURE greaterInt*(left, right: Code.PExpression; rtl: OberonRtl.PType): Code.PExpression;
    RETURN relational(left, right, rtl, opGreaterInt, " > ")
END greaterInt;

PROCEDURE greaterReal*(left, right: Code.PExpression; rtl: OberonRtl.PType): Code.PExpression;
    RETURN relational(left, right, rtl, opGreaterReal, " > ")
END greaterReal;

PROCEDURE greaterStr*(left, right: Code.PExpression; rtl: OberonRtl.PType): Code.PExpression;
    RETURN strCmp(" > ", left, right, rtl)
END greaterStr;

PROCEDURE eqLessInt*(left, right: Code.PExpression; rtl: OberonRtl.PType): Code.PExpression;
    RETURN relational(left, right, rtl, opEqLessInt, " <= ")
END eqLessInt;

PROCEDURE eqLessReal*(left, right: Code.PExpression; rtl: OberonRtl.PType): Code.PExpression;
    RETURN relational(left, right, rtl, opEqLessReal, " <= ")
END eqLessReal;

PROCEDURE eqLessStr*(left, right: Code.PExpression; rtl: OberonRtl.PType): Code.PExpression;
    RETURN strCmp(" <= ", left, right, rtl)
END eqLessStr;

PROCEDURE eqGreaterInt*(left, right: Code.PExpression; rtl: OberonRtl.PType): Code.PExpression;
    RETURN relational(left, right, rtl, opEqGreaterInt, " >= ")
END eqGreaterInt;

PROCEDURE eqGreaterReal*(left, right: Code.PExpression; rtl: OberonRtl.PType): Code.PExpression;
    RETURN relational(left, right, rtl, opEqGreaterReal, " >= ")
END eqGreaterReal;

PROCEDURE eqGreaterStr*(left, right: Code.PExpression; rtl: OberonRtl.PType): Code.PExpression;
    RETURN strCmp(" >= ", left, right, rtl)
END eqGreaterStr;

PROCEDURE not*(x: Code.PExpression; rtl: OberonRtl.PType): Code.PExpression;
    RETURN unary(x, opNot, "!")
END not;

PROCEDURE negateInt*(x: Code.PExpression; rtl: OberonRtl.PType): Code.PExpression;
    RETURN promoteToWideIfNeeded(unary(x, opNegateInt, "-"))
END negateInt;

PROCEDURE negateReal*(x: Code.PExpression; rtl: OberonRtl.PType): Code.PExpression;
    RETURN promoteToWideIfNeeded(unary(x, opNegateReal, "-"))
END negateReal;

PROCEDURE unaryPlus*(x: Code.PExpression; rtl: OberonRtl.PType): Code.PExpression;
    RETURN unary(x, opUnaryPlus, "")
END unaryPlus;

PROCEDURE setComplement*(x: Code.PExpression; rtl: OberonRtl.PType): Code.PExpression;
    RETURN unary(x, opSetComplement, "~")
END setComplement;

PROCEDURE lsl*(left, right: Code.PExpression; rtl: OberonRtl.PType): Code.PExpression;
    RETURN binaryWithCode(left, right, rtl, opLsl, " << ", Precedence.shift)
END lsl;

PROCEDURE asr*(left, right: Code.PExpression; rtl: OberonRtl.PType): Code.PExpression;
    RETURN binaryWithCode(left, right, rtl, opAsr, " >> ", Precedence.shift)
END asr;

PROCEDURE ror*(left, right: Code.PExpression; rtl: OberonRtl.PType): Code.PExpression;
    RETURN binaryWithCode(left, right, rtl, opRor, " >>> ", Precedence.shift)
END ror;

PROCEDURE mulInplace*(left, right: Code.PExpression; cx: LanguageContext.Type): STRING;
    RETURN inplace(left, right, cx, " *= ", mulReal)
END mulInplace;

PROCEDURE divInplace*(left, right: Code.PExpression; cx: LanguageContext.Type): STRING;
    RETURN inplace(left, right, cx, " /= ", divReal)
END divInplace;

PROCEDURE pow2*(e: Code.PExpression): Code.PExpression;
VAR
    derefExp: Code.PExpression;
BEGIN
    derefExp := Code.derefExpression(e);
    RETURN Code.makeSimpleExpression("Math.pow(2, " + derefExp.code() + ")",
                                     Types.basic.real)
END pow2;

PROCEDURE log2*(e: Code.PExpression): Code.PExpression;
VAR
    derefExp: Code.PExpression;
BEGIN
    derefExp := Code.derefExpression(e);
    RETURN Code.makeExpressionWithPrecedence(
            "(Math.log(" + derefExp.code() + ") / Math.LN2) | 0",
            Types.basic.integer,
            NIL,
            NIL,
            Precedence.bitOr)
END log2;

PROCEDURE opCastToUint8(left, right: Code.PConst): Code.PConst;
    RETURN Code.makeIntConst(left^(Code.IntConst).value 
                           * right^(Code.IntConst).value)
END opCastToUint8;

PROCEDURE CastToUint8.make(rtl: OberonRtl.PType; e: Code.PExpression): Code.PExpression;
    RETURN binaryWithCode(
        e, 
        Code.makeExpression("0xFF", 
                            Types.basic.integer, 
                            NIL, 
                            Code.makeIntConst(0FFH)), 
        rtl, 
        opCastToUint8, 
        " & ", 
        Precedence.bitAnd)
END CastToUint8.make;

BEGIN
    openArrayChar := Types.makeArray("", "", Types.basic.ch, Types.openArrayLength);

    NEW(castToUint8);
    castOperations.castToUint8 := castToUint8;
END Operator.